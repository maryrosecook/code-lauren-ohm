CodeLauren {
  Start = ExpressionList

  ExpressionList = listOf<Expression, eol>
  Expression = Forever | Invocation | Conditional | Assignment | Lambda | Literal | identifier

  Lambda = "{" ParameterList ExpressionList "}"

  Invocation = Invocable "(" ArgumentList ")"
  ArgumentList = listOf<Expression, space>

  Invocable = Conditional | Lambda | identifier

  ParameterList = listOf<Parameter, space>
  Parameter = "?" identifier

  Assignment = identifier ":" Expression

  Forever = "forever" Lambda

  Conditional = If
  If = "if" Expression Lambda (Elseif | Else)*
  Elseif = "elseif" Expression Lambda (Elseif | Else)*
  Else = "else" Lambda

  Literal = number | string | boolean

  number = "-"? digit+ ("." digit+)?

  string = "\"" (alnum | "-" | "." | "," | "#" | space)* "\""

  identifier = ~keyword letter alnum*

  boolean = "true" | "false"

  keyword = "if" | "elseif" | "else" | "forever" | "true" | "false"

  eol = space_no_newline* "\n" space_no_newline*
  space_no_newline = " " | "\r" | "\t"
}
